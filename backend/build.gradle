plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.org.apache.groovy.groovy
    implementation libs.org.apache.groovy.groovy.json
    implementation libs.org.apache.groovy.groovy.xml
    implementation libs.org.apache.groovy.groovy.templates

    implementation libs.org.postgresql.postgresql

    testImplementation libs.org.junit.jupiter.junit.jupiter
    testImplementation libs.org.junit.jupiter.junit.jupiter.api
    testImplementation libs.org.junit.jupiter.junit.jupiter.engine
    testImplementation libs.org.junit.jupiter.junit.jupiter.params

    testImplementation libs.org.spockframework.spock.core
}

group = 'com.example'
version = '1.0-SNAPSHOT'
description = 'Linketinder'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

application {
    mainClass = 'Main'
}

run {
    standardInput = System.in
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}